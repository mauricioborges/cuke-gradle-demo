apply plugin: 'groovy'

test.testLogging.exceptionFormat = 'full'

repositories {
    mavenCentral()
}


configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

ext {
   steps_definition_path='src/test/steps/'
   different_cucumber_steps=file(steps_definition_path).listFiles().collect{it.name}
}


different_cucumber_steps.each {kind_of_testing ->
  task("cucumber_$kind_of_testing")  {
      description "Run over feature behaviours according to the steps defined to access the application using the $kind_of_testing"
      dependsOn assemble
      doLast {
          javaexec {
              main = "cucumber.api.cli.Main"
              classpath = configurations.cucumberRuntime
              args = ['--strict', '-f', "pretty", '-f', "html:build/reports/cucumber",  '--glue',"$steps_definition_path/$kind_of_testing", 'src/test/cucumber' ]
  
          }
      }
  }

}

dependencies {
	compile group: 'org.codehaus.groovy', name: 'groovy', version: '2.3.0'
        //due to a bug in Gradle: http://issues.gradle.org/browse/GRADLE-732
        cucumberRuntime files("${jar.archivePath}")
	testCompile 'junit:junit:4.11'
	testCompile 'info.cukes:cucumber-groovy:1.1.8'
	testCompile 'info.cukes:cucumber-junit:1.1.8'
        testCompile 'org.gebish:geb-core:0.9.3'
        testCompile 'org.seleniumhq.selenium:selenium-firefox-driver:2.42.2'

  	testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'

	testCompile ('org.codehaus.groovy.modules.http-builder:http-builder:0.7')
	testCompile ('oauth.signpost:signpost-core:1.2.1.2')
	testCompile ('oauth.signpost:signpost-commonshttp4:1.2.1.2')
 


}

sourceSets{
	test{
		groovy {
			srcDirs = ['src/test/bugs','src/test/resources/runners']
		}
	
	}
}

